import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
}
apply from: "$project.rootDir/jacoco/jacoco.gradle"

android {
    namespace 'com.cyd.benchmark'
    compileSdk 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["androidx.benchmark.suppressErrors"] = 'EMULATOR'

    }

    buildTypes {
        // This benchmark buildType is used for benchmarking, and should function like your
        // release build (for example, with minification on). It's signed with a debug key
        // for easy local/CI testing.
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
            matchingFallbacks = ["release"]
        }
    }

    targetProjectPath = ":app"
    experimentalProperties["android.experimental.self-instrumenting"] = true

    testOptions {
        managedDevices {
            devices {
                pixel2Api31(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
            }
        }
    }
}

dependencies {
    implementation project(path: ':core:ui')

    implementation "androidx.test.ext:junit:$junit_version"
    implementation "androidx.test.espresso:espresso-core:$espresso_core_version"
    implementation "androidx.test.uiautomator:uiautomator:$uiautomator_version"
    implementation "androidx.benchmark:benchmark-macro-junit4:$macro_benchmark_version"
    implementation "androidx.profileinstaller:profileinstaller:$profile_installer_version"
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == "benchmark"
    }
}